N_BATCHES = 200

#Extract trascript tags from the GTF file
rule extract_tags:
	input:
		gtf = "processed/{annotation}.gtf.gz"
	output:
		tags = "processed/{annotation}.transcript_tags.txt"
	threads: 1
	resources:
		mem = 1000
	shell:
		"""
		source activate py3.6
		python extractTranscriptTags.py --gtf {input.gtf} > {output.tags}
		"""

#Prepare annotationns
rule prepare_annotations:
	input:
		gtf = "processed/{annotation}.gtf.gz",
		tags = "processed/{annotation}.transcript_tags.txt"
	output:
		annot = "processed/{annotation}_txrevise_annotations.rds"
	threads: 1
	resources:
		mem = 6000
	shell:
		"""
		module load R-3.4.1
		Rscript prepareAnnotations.R --gtf {input.gtf} --tags {input.tags} --out {output.annot}
		"""

#Construct events
rule construct_events:
	input:
		annot = "processed/{annotation}_txrevise_annotations.rds"
	output:
		"processed/{annotation}/txrevise.grp_1.upstream.{batch}_{n_batches}.gff3",
		"processed/{annotation}/txrevise.grp_2.upstream.{batch}_{n_batches}.gff3",
		"processed/{annotation}/txrevise.grp_1.contained.{batch}_{n_batches}.gff3",
		"processed/{annotation}/txrevise.grp_2.contained.{batch}_{n_batches}.gff3",
		"processed/{annotation}/txrevise.grp_1.downstream.{batch}_{n_batches}.gff3",
		"processed/{annotation}/txrevise.grp_2.downstream.{batch}_{n_batches}.gff3"
	params:
		batch_str = "'{batch} {n_batches}'",
		outdir = "processed/{annotation}"
	threads: 1
	resources:
		mem = 4000
	shell:
		"""
		module load R-3.4.1
		Rscript constructEvents.R --annot {input.annot} --batch {params.batch_str} --out {params.outdir} --fill TRUE
		"""

#Merge txrevise output files
rule merge_gff_files:
	input:
		gff = expand("processed/{{annotation}}/txrevise.{{group}}.{{position}}.{batch}_{n_batches}.gff3",
			batch = [i for i in range(1, N_BATCHES + 1)],
			n_batches = N_BATCHES)
	output:
		gff = "processed/{annotation}/merged/txrevise.{group}.{position}.gff3"
	threads: 1
	resources:
		mem = 1000
	shell:
		'cat {input.gff} | grep -v "^#" > {output.gff}'


#Iterate over grups and positions
rule make_all:
	input:
		gff = expand("processed/{{annotation}}/merged/txrevise.{group}.{position}.gff3", 
			group = ["grp_1", "grp_2"], 
			position = ["upstream", "contained", "downstream"])
	output:
		"processed/{annotation}_log.txt"
	threads: 1
	resources:
		mem = 1000
	shell:
		"echo 'Done!' > {output}"
